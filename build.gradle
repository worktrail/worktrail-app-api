
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'net.worktrail';
version = '0.2.0'




repositories {
  mavenCentral();
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign configurations.archives
}


uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'worktrail-app-api'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Implementation of Developer API for WorkTrail, a time tracking app.'
        url 'https://github.com/worktrail/worktrail-app-api'

        scm {
          connection 'scm:git:git@github.com:worktrail/worktrail-app-api.git'
          developerConnection 'scm:git:git@github.com:worktrail/worktrail-app-api.git'
          url 'git@github.com:worktrail/worktrail-app-api.git'
        }

        licenses {
          license {
            name 'The BSD License'
            url 'http://www.opensource.org/licenses/bsd-license.php'
          }
        }

        developers {
          developer {
            id 'hpoul'
            name 'Herbert Poul'
            email 'herbert.poul@worktrail.net'
          }
        }
      }
    }
  }
}



dependencies {
	compile 'org.json:json:20090211'
	compile 'com.google.guava:guava:15.0'
	
	
	testCompile 'junit:junit:4.11' 
}

